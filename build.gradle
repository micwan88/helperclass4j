/*
 * This build file was generated by the Gradle 'init' task.
 *
 * This generated file contains a sample Java Library project to get you started.
 * For more details take a look at the Java Libraries chapter in the Gradle
 * user guide available at https://docs.gradle.org/3.5/userguide/java_library_plugin.html
 */

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

group = 'io.github.micwan88'
version = '2.0.1-SNAPSHOT'

ext.releaseVer = !version.endsWith("SNAPSHOT")

java {
    withJavadocJar()
    withSourcesJar()
}

repositories {
    mavenCentral()
}

dependencies {
    //Http Client
    api 'org.apache.httpcomponents:httpclient:4.5.14'
    
    //Log4j2
    implementation 'org.apache.logging.log4j:log4j-api:2.19.0'
    implementation 'org.apache.logging.log4j:log4j-core:2.19.0'

    //DBCP
    implementation 'org.apache.commons:commons-dbcp2:2.9.0'
    
    //Common Codec
    testImplementation 'commons-codec:commons-codec:1.15'

    // Use JUnit test framework
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.1'
}

tasks.named('jar') {
    manifest {
        attributes('Implementation-Title': project.name, 
            'Implementation-Version': project.version,
            'Automatic-Module-Name': project.group + '.' + project.name)
    }
}

tasks.named('test') {
    useJUnitPlatform() 
}

publishing {
    publications {
        helperclass4j(MavenPublication) {
            from components.java

            pom {
                description = 'This is a java helper class library for the author (Michael Wan) self usage'
                url = 'https://github.com/micwan88/helperclass4j'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://github.com/micwan88/helperclass4j/blob/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'micwan88'
                        name = 'Michael Wan'
                        email = 'hkmicmic@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/micwan88/helperclass4j.git'
                    developerConnection = 'scm:git:git@github.com:micwan88/helperclass4j.git'
                    url = 'https://github.com/micwan88/helperclass4j'
                }
            }
        }
    }

    repositories {
        maven {
            def releasesRepoUrl = 'https://s01.oss.sonatype.org/content/repositories/releases'
            def snapshotsRepoUrl = 'https://s01.oss.sonatype.org/content/repositories/snapshots'
            url = releaseVer ? releasesRepoUrl : snapshotsRepoUrl
        }
    }
}

signing {
    required { releaseVer }
    sign publishing.publications.helperclass4j
}